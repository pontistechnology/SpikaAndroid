apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: "kotlin-parcelize"
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'androidx.navigation.safeargs.kotlin'

android {
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFileName = "spika_ultimate_${variant.versionCode}_${variant.buildType.name}.apk"
        }
    }

    def localProperties = new Properties()
    localProperties.load(new FileInputStream(rootProject.file("local.properties")))

    signingConfigs {
        debug {
            storeFile file(localProperties['debugKeystorePath'])
        }
        release {
            storeFile file(localProperties['releaseKeystorePath'])
            storePassword localProperties['releaseKeystorePassword']
            keyAlias localProperties['releaseKeystoreKeyAlias']
            keyPassword localProperties['releaseKeystoreKeyPassword']
        }
        messenger {
            storeFile file(localProperties['messengerKeystorePath'])
            storePassword localProperties['messengerKeystorePassword']
            keyAlias localProperties['messengerKeystoreKeyAlias']
            keyPassword localProperties['messengerKeystoreKeyPassword']
        }
    }
    compileSdk 33

    buildFeatures {
        viewBinding true
    }

    defaultConfig {
        applicationId "com.clover.studio.spikamessenger"
        minSdk 24
        targetSdk 33
        versionCode 100028
        versionName "1.0.0"

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas")
            }
        }

        sourceSets {
            // Adds exported schema location as test app assets.
            androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
        }


        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        // debug variant is used for development
        debug {
            applicationIdSuffix ".dev"
            versionNameSuffix "_debug"

            debuggable true

            resValue "string", "APP_NAME", "Spika3 Dev"
            buildConfigField("String", "SERVER_URL", "\"https://dev3.spika.chat/\"")

            signingConfig signingConfigs.debug
        }

        dev {
            applicationIdSuffix ".dev"
            versionNameSuffix "_dev"

            debuggable true

            resValue "string", "APP_NAME", "Spika3 Dev"
            buildConfigField("String", "SERVER_URL", "\"https://dev3.spika.chat/\"")

            signingConfig signingConfigs.debug
        }

        releaseDebug {
            applicationIdSuffix ".release"
            versionNameSuffix "_releaseDebug"

            debuggable true
            minifyEnabled false

            resValue "string", "APP_NAME", "Spika3"
            buildConfigField("String", "SERVER_URL", "\"https://clover.spika.chat/\"")

            signingConfig signingConfigs.debug
        }

        release {
            applicationIdSuffix ".release"

            debuggable false
            minifyEnabled false

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            resValue "string", "APP_NAME", "Spika3"
            buildConfigField("String", "SERVER_URL", "\"https://clover.spika.chat/\"")

            signingConfig signingConfigs.release
        }

        messengerDebug {
            applicationIdSuffix ".messenger"
            versionNameSuffix "_messengerDebug"

            debuggable true
            minifyEnabled false

            resValue "string", "APP_NAME", "Spika Messenger Dev"
            buildConfigField("String", "SERVER_URL", "\"https://messenger-dev.spika.chat/\"")

            signingConfig signingConfigs.debug
        }

        messenger {
            applicationIdSuffix ".messenger"

            debuggable false
            minifyEnabled false

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            resValue "string", "APP_NAME", "Spika Messenger"
            buildConfigField("String", "SERVER_URL", "\"https://messenger-dev.spika.chat/\"")

            signingConfig signingConfigs.messenger
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.6.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.firebase:firebase-messaging-ktx:23.1.1'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    //Crashlytics
    implementation platform('com.google.firebase:firebase-bom:30.0.0')
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'

    //Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava3:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.2'

    //Lifecycle
    def lifecycle_version = "2.5.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    //Kotlin Coroutines
    def coroutines_android_version = '1.5.2'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_android_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_android_version"

    //Hilt
    implementation 'com.google.dagger:hilt-android:2.40.5'
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha03"
    kapt 'com.google.dagger:hilt-android-compiler:2.40.5'
    kapt "androidx.hilt:hilt-compiler:1.0.0"

    //Room
    def room_version = "2.5.0"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    androidTestImplementation "androidx.room:room-testing:$room_version"

    //Navigation
    def nav_version = "2.5.3"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    //Glide
    def glide_version = "4.12.0"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    implementation "com.github.bumptech.glide:annotations:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    //Timber
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // LibPhoneNumber
    implementation 'com.googlecode.libphonenumber:libphonenumber:8.12.41'

    // SmsVerificationAPI
    implementation 'com.google.android.gms:play-services-auth:20.4.1'
    implementation 'com.google.android.gms:play-services-auth-api-phone:18.0.1'

    // Emoji
    def emojiVersion = "0.15.0"
    implementation "com.vanniktech:emoji-google:$emojiVersion"

    // TouchImageView
    implementation 'com.github.MikeOrtiz:TouchImageView:1.4.1'

    // Exo Player
    def mediaVersion = "1.0.0-rc01"
    dependencies {
        implementation "androidx.media3:media3-exoplayer:$mediaVersion"
        implementation "androidx.media3:media3-ui:$mediaVersion"
        implementation "androidx.media3:media3-exoplayer-dash:$mediaVersion"
    }

    // Markdown
    implementation "io.noties.markwon:core:4.6.2"
}